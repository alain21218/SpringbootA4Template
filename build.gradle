buildscript {
	ext {
		springBootVersion = '1.5.9.RELEASE'
	}
	repositories {
		mavenCentral()
        maven{
            url "https://plugins.gradle.org/m2/"
            maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }
        }
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.moowork.gradle:gradle-node-plugin:1.1.1"
	}
}

apply plugin: "com.moowork.node"

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'fr.asoumagne'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("com.h2database:h2")
	compile('org.springframework.boot:spring-boot-starter-web')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

node{
	version = '8.9.3'
	download = true
    nodeModulesDir = file("client")
}

task buildClientDev(type: NpmTask, dependsOn: 'npmInstall'){
	group = 'build'
	description = 'Compile client side assets for development'
	args = ['run', 'buildDev']
}

task buildClient(type: NpmTask, dependsOn: 'npmInstall'){
	group = 'build'
	description = 'Compile client side assets for production'
	args = ['run', 'build']
}

task buildClientWatch(type: NpmTask, dependsOn: 'npmInstall'){
	group = 'application'
	description = 'Builds and watches the client assets for rebuilding'
	args = ['run', 'buildWatch']
}

task clientTest(type: NpmTask, dependsOn: 'npmInstall'){
	group = 'verification'
	description = 'Executes client side unit tests'
	args = ['run', 'test']
}

bootRun.dependsOn(buildClientDev)

jar.dependsOn(buildClient)